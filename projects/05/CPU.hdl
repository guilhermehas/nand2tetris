/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Mux16(a=AluOut, b=instruction, sel=notInst, out=Ain);

    Or(a=notInst, b=instruction[5], out=loadA);
    ARegister(in=Ain, load=loadA, out=Aout, out[0..14]=addressM);

    And(a=instruction[15], b=instruction[12], out=selAluA);
    Mux16(a=Aout, b=inM, sel=selAluA, out=AluA);

    And(a=instruction[15], b=instruction[4], out=loadD);
    DRegister(in=AluOut, load=loadD, out=AluD);

    Or(a=notInst, b=instruction[11], out=zx);
    Or(a=notInst, b=instruction[10], out=nx);
    And(a=instruction[15], b=instruction[9], out=zy);
    And(a=instruction[15], b=instruction[8], out=ny);
    And(a=instruction[15], b=instruction[7], out=f);
    And(a=instruction[15], b=instruction[6], out=no);

    ALU(x=AluD, y=AluA, 
        zx=zx, nx=nx, zy=zy, ny=ny, f=f, no=no,
        out=outM, out=AluOut, zr=zr, ng=ng);
    
    Or(a=zr, b=ng, out=negZ);
    Not(in=negZ, out=pos);

    And(a=instruction[0], b=pos, out=j1);
    And(a=instruction[1], b=zr, out=j2);
    And(a=instruction[2], b=ng, out=j3);

    Or(a=j1, b=j2, out=j12);
    Or(a=j12, b=j3, out=j123);
    And(a=j123, b=instruction[15], out=loadPC);

    PC(in=Aout, reset=reset, load=loadPC, inc=true, out[0..14]=pc);

    And(a=instruction[15], b=instruction[3], out=writeM);

    Not(in=instruction[15], out=notInst);
}
